ACTIVITIES = ["ups", "jog"]
PARTICIPANTS = [0, 1]
RAW_DATA_DIR = "../../data"
ACT_DATA_DIR = "act_data"
CHECKPOINTS_DIR = "checkpoints"
RESULT_DIR = "results"
# dir to pipeline configs is "config"

rule all_train:
    input:
        expand(
            f"tmp/{CHECKPOINTS_DIR}/subj_{{subj_id}}/{{act}}_checkpoint.ckpt",
            act=ACTIVITIES, subj_id=PARTICIPANTS
        )

rule all_data:
    input:
        expand(
            f"tmp/{ACT_DATA_DIR}/subj_{{subj_id}}/{{act}}/train.pkl", 
            act=ACTIVITIES, subj_id=PARTICIPANTS
        )

rule all_wandb_configs:
    input:
        expand("config/subj_{subj_id}/wandb.yaml", subj_id=PARTICIPANTS)

rule all:
    input:
        f"{RESULT_DIR}/agr_metrics.csv",
        f"{RESULT_DIR}/recall_hist.html"

rule make_results:
    input:
        "scripts/collect_metrics.py",
        config_wandb_path = "config/wandb.yaml",
        classify_table = "tmp/classify_table.csv"
    output:
        f"{RESULT_DIR}/agr_metrics.csv",
        f"{RESULT_DIR}/recall_hist.html"
    shell: f"""
        python scripts/collect_metrics.py {{input.classify_table}} \
        {RESULT_DIR} {{input.config_wandb_path}}
    """

rule classify:
    input:
        "scripts/classify.py",
        expand(f"tmp/{CHECKPOINTS_DIR}/{{act}}_checkpoint.ckpt", act=ACTIVITIES),
        expand(f"tmp/{ACT_DATA_DIR}/{{act}}/test.pkl", act=ACTIVITIES),
        expand(f"config/{{act}}/train.yaml", act=ACTIVITIES),
        config_wandb_path = "config/wandb.yaml"
    output:
        "tmp/classify_table.csv"
    shell:
        f"""
        python scripts/classify.py tmp/{ACT_DATA_DIR} tmp/{CHECKPOINTS_DIR} \
        {{input.config_wandb_path}}
        """

rule train:
    input:
        "scripts/train_on_activity.py",
        train_dataset = f"tmp/{ACT_DATA_DIR}/subj_{{subj_id}}/{{act}}/train.pkl",
        test_dataset = f"tmp/{ACT_DATA_DIR}/subj_{{subj_id}}/{{act}}/test.pkl",
        config_train = "config/{act}/train.yaml",
        config_data = "config/{act}/data.yaml",
        config_wandb_path = "config/subj_{subj_id}/wandb.yaml"
    output:
        f"tmp/{CHECKPOINTS_DIR}/subj_{{subj_id}}/{{act}}_checkpoint.ckpt"
    shell:
        f"""
        python scripts/train_on_activity.py {{input.config_train}} \
        {{input.config_data}} {{input.train_dataset}} {{input.test_dataset}} \
        {{input.config_wandb_path}} tmp/{CHECKPOINTS_DIR}/subj_{{wildcards.subj_id}}
        """

rule make_dataset:
    input:
        "scripts/make_dataset.py",
        config_path = "config/{act}/data.yaml",
        shared_config_path = "config/data.yaml",
        config_wandb_path = "config/subj_{subj_id}/wandb.yaml"
    output:
        f"tmp/{ACT_DATA_DIR}/subj_{{subj_id}}/{{act}}/train.pkl",
        f"tmp/{ACT_DATA_DIR}/subj_{{subj_id}}/{{act}}/test.pkl",
    shell:
        f"""
        python scripts/make_dataset.py {{input.config_path}} \
        {{input.shared_config_path}} {{input.config_wandb_path}} {RAW_DATA_DIR} \
        tmp/{ACT_DATA_DIR}/subj_{{wildcards.subj_id}}/{{wildcards.act}}
        """

rule configure_wandb:
    input:
        "scripts/wandb_config.py"
    output:
        "config/subj_{subj_id}/wandb.yaml"
    script:
        "scripts/wandb_config.py"
